def version = "3.6.0"
def filename = "fineuploader"
def srcDir = 'client'
def jsSrcDir = "$srcDir/js"
def tempSrcDir = 'build/temp'

apply plugin: 'js'

def coreFiles(String dir) {["${dir}/header.js",
                 "${dir}/util.js",
                 "${dir}/features.js",
                 "${dir}/promise.js",
                 "${dir}/button.js",
                 "${dir}/paste.js",
                 "${dir}/uploader.basic.js",
                 "${dir}/dnd.js",
                 "${dir}/uploader.js",
                 "${dir}/ajax.requester.js",
                 "${dir}/deletefile.ajax.requester.js",
                 "${dir}/window.receive.message.js",
                 "${dir}/handler.base.js",
                 "${dir}/handler.form.js",
                 "${dir}/handler.xhr.js"]
}

def cssFiles = ["$srcDir/fineuploader.css"]

def imageFiles = ["$srcDir/loading.gif",
                  "$srcDir/processing.gif"]

def additionalFilesToInclude(String dir) { ["${dir}/iframe.xss.response.js"] }

def jQueryPluginFiles(String dir) { ["${dir}/jquery-plugin.js",
                         "${dir}/jquery-dnd.js"] }

def synchronized orderTasks( String aTasks ) {
    // Laurence Toenjes 2012
    // preserve dependency task order for Gradle (put this near the top of your gradle build file)
    // example: task( 'seq-alpha', dependsOn: orderTasks( 'alpha bravo charlie' ) ) << { /* your task code */ }
    // To view task dependency do cmd: gradle tasks --all
    if ( !(project.ext.properties.containsKey("orderTasksCtr")) )
        project.ext.orderTasksCtr = new java.util.concurrent.atomic.AtomicInteger(1000);
    def cl_createProxyTask = { aTaskName ->
        def ctr         = project.ext.orderTasksCtr.incrementAndGet();
        def newTaskName = "_seq_${ctr}_${aTaskName}";
        def newTask     = project.task( newTaskName );
        newTask.dependsOn << "${aTaskName}";
        def result = newTask;
    }
    ''' , ' " [ ] ( ) '''.trim().split().each {
        if (it in aTasks)
            aTasks = aTasks.replace( it, (32 as char).toString() )
    }
    return aTasks.split().collect { cl_createProxyTask(it) };
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.3'
    }
}

combineJs {
    source = files(coreFiles(tempSrcDir))
    dest = file("$buildDir/${filename}-${version}.js")
}

task combineJsJquery(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = files(coreFiles(tempSrcDir))  + files(jQueryPluginFiles(tempSrcDir))
    dest = file("$buildDir/jquery.${filename}-${version}.js")
}

minifyJs {
    source = combineJs
    dest = file("$buildDir/${filename}-${version}.min.js")
}

task minifyJsJquery(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
    source = combineJsJquery
    dest = file("$buildDir/jquery.${filename}-${version}.min.js")
}

task zipJquery(type: Zip) {
    baseName "jquery.$filename-$version"
    from files("$buildDir")
    destinationDir file('release')
}

task zip(type: Zip) {
    baseName "$filename-$version"
    from files("$buildDir")
    destinationDir file('release')
}

task copyResources(type:Copy) {
    from cssFiles + imageFiles + additionalFilesToInclude(jsSrcDir)
    into 'build'
    rename { String fileName ->
        fileName.replace('.css', "-${version}.css")
    }
    rename { String fileName ->
        fileName.replace('.js', "-${version}.js")
    }
}

task clean << {
    delete files("$buildDir")
    delete file(tempSrcDir)
}

task deleteTemp << {
    delete file(tempSrcDir)
}

task copyJsToTempDir(type:Copy) {
    from coreFiles(jsSrcDir) + jQueryPluginFiles(jsSrcDir)
    into tempSrcDir
}

task stampWithVersion << {
    File headerFile = file("$tempSrcDir/header.js")
    String newContents = headerFile.text.replaceFirst("Version:", "Version: $version")
    headerFile.text = newContents
}

task createJQueryRelease(dependsOn: orderTasks('clean copyJsToTempDir stampWithVersion minifyJsJquery copyResources deleteTemp zipJquery')) {
    outputs.upToDateWhen { false }
}

task createRelease(dependsOn: orderTasks('clean copyJsToTempDir stampWithVersion minifyJs copyResources deleteTemp zip')) {
    outputs.upToDateWhen { false }
}
